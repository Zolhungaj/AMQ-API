plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'maven-publish'
}

group = 'tech.zolhungaj'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/Zolhungaj/AMQ-API"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		//noinspection GroovyAssignabilityCheck
		gpr(MavenPublication) {
			//noinspection GroovyAssignabilityCheck
			artifactId 'amq-api'
			from(components.java)
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.json:json:20220924'
	implementation('com.squareup.moshi:moshi:1.14.0')
	implementation ('io.socket:socket.io-client:1.0.2') {
		// excluding org.json because the one imported in socket-io has flaws
		exclude group: 'org.json', module: 'json'
	}
	implementation 'org.eclipse.jetty:jetty-reactive-httpclient'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

tasks.named('test') {
	useJUnitPlatform()
}
